buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0-rc1'
    }
}
apply plugin: 'com.android.library'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        versionCode project.ext.versionCodeInt
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    provided 'com.android.support:appcompat-v7:21.0.2'
}

//
// Attaches a Jar task to the archive artifact. Makes sure we upload a Jar file to maven.
//
android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    // Only attach a jar for non-debug build types.
    if (!name.equals("DEBUG")) {
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }
}

apply from: '../gradle/deploy.gradle'
